group 'br.com.felipe'
version '1.0-SNAPSHOT'

buildscript {
    ext {
        springBootVersion = '1.4.0.RELEASE'
    }
    repositories {
        maven { url "http://repo.spring.io/snapshot" }
        maven { url "http://repo.spring.io/milestone" }
        jcenter()
    }
    dependencies {
        classpath "net.saliman:gradle-cobertura-plugin:2.3.2"
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'spring-boot'

dependencies {
    compile project(":config")
    compile('org.springframework.boot:spring-boot-starter-data-rest')
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

jar {
    baseName = 'JsonDiffer'
    version = '1.0-SNAPSHOT'
    manifest {
        attributes 'Main-Class': 'br.com.felipe.Application'
    }
}

/**
 * Applying coverage tests to subprojects
 */
subprojects {
    apply plugin: 'cobertura'

    repositories {
        jcenter()
    }

    cobertura {
        coverageIgnoreTrivial = true
        coverageFormats = ["xml", "html"]
    }
    test.useTestNG()
}

/**
 * Run the main application
 */
bootRun {
    main = 'br.com.felipe.Application'
}

/**
 * Confs to all projects
 */
allprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'spring-boot'
    apply plugin: 'jacoco'

    sourceCompatibility = 1.8
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url "http://repo1.maven.org/maven2/" }
    }
    dependencies {
        compile('org.springframework.boot:spring-boot-starter-data-rest')
        testCompile group: 'junit', name: 'junit', version: '4.11'
    }
    test.useTestNG()

    test {
        testLogging {
            // set options for log level LIFECYCLE
            events "failed"
            exceptionFormat "short"

            // set options for log level DEBUG
            debug {
                events "started", "skipped", "failed"
                exceptionFormat "full"
            }

            // remove standard output/error logging from --info builds
            // by assigning only 'failed' and 'skipped' events
            info.events = ["failed", "skipped"]
        }
    }

    sourceSets {
        test {
            java {
                srcDirs = ["test/br/com/felipe"]
            }
        }
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}

/**
 * wrapper to run on travis
 */
task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}


